/*
 * Copyright (c) 2019, WSO2 Inc. (http://www.wso2.org) All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */
 
apply from: "$rootDir/gradle/javaProjectWithExtBalo.gradle"

configurations {
    balSource {
        transitive false
    }
}

class CreateBaloTask extends JavaExec {
    def isBuiltin = 'false'
    def skipWarnings = 'false'
    def jvmTarget = 'false'

    CreateBaloTask() {
        setClasspath(project.configurations.baloCreat)
        setMain('org.ballerinalang.stdlib.utils.GenerateBalo')
        setEnableAssertions(true)
    }


    @Override
    void exec() {
        def args = []
        args << isBuiltin
        args << "src/main/ballerina/"
        args << "${project.buildDir}/generated-balo/repo/ballerina"
        args << "${project.buildDir}/ballerina-home/main"
        args << skipWarnings
        args << jvmTarget
        super.setArgs(args)
        super.exec()
    }

    @Override
    JavaExec args(Object... args) {
        throw new UnsupportedOperationException()
    }

    @Override
    JavaExecSpec args(Iterable<?> args) {
        throw new UnsupportedOperationException()
    }

}

task createBalo(type: CreateBaloTask) {
    dependsOn createBalHome
    dependsOn configurations.baloImplementation

    def balFiles = file("src/main/ballerina/")
    inputs.dir(balFiles)
    inputs.files(configurations.baloImplementation)
    outputs.dir("$buildDir/generated-balo")
    outputs.cacheIf { true }
}

artifacts {
    baloImplementation file("$buildDir/generated-balo/"), { builtBy(createBalo) }
    balSource file("src/main/ballerina/")
}

task copySelfToTestBalHome(type: Copy) {
    dependsOn createBalo
    from "$buildDir/generated-balo"
    into "$buildDir/ballerina-home/test/lib"
}

task assembleArtifact(type: Zip) {
    dependsOn createBalo
    archiveName "$project.name-ballerina-binary-repo.zip"
    destinationDir file("$buildDir/bal-repos/")
    from "build/generated-balo/"
    description "Assemble archive $archiveName into ${relativePath(destinationDir)}"
    outputs.dir("$buildDir/bal-repos/")
}

test {
    dependsOn copySelfToTestBalHome
}

publishing {
    publications {
        mavenJava {
            artifact source: assembleArtifact, classifier: "ballerina-binary-repo", extension: 'zip'
        }
    }
}

apply from: "$rootDir/gradle/birProject.gradle"
