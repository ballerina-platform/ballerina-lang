/*
 * Copyright (c) 2020, WSO2 Inc. (http://www.wso2.org) All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

apply from: "$rootDir/gradle/javaProject.gradle"


configurations {
    distributionBala {
        transitive false
    }

    balDoc {
        transitive false
    }

    distributionBirJar {
        transitive false
    }
    balaCreat {
        transitive false
    }
}

dependencies {
    implementation project(':ballerina-lang')
    balaCreat project(':lib-creator')
}

@CacheableTask
class BallerinaLangLibBuildTask extends JavaExec {
    @Internal def projectDir = project.layout.buildDirectory.dir("ballerina-src")
    @Internal def distCache = project.layout.buildDirectory
    @Internal def pkgName = project.name
    @Internal def skipBootstrap = 'false'
    @Internal def outputJarFileName = ''

    @InputFiles
    @PathSensitive(PathSensitivity.RELATIVE)
    def srcDir = project.files("src/main/ballerina")

    @OutputFile
    def zipFile = projectDir.map{ it.file("target/" + project.projectDir.name + ".zip") }

    @OutputFile
    def jarFile = projectDir.map{ it.file("target/" + outputJarFileName + ".jar") }

    @Override @Internal
    List<CommandLineArgumentProvider> getJvmArgumentProviders() {
        return super.getJvmArgumentProviders()
    }

    BallerinaLangLibBuildTask() {
        setMainClass('org.ballerinalang.stdlib.utils.BuildLangLib')
        standardOutput = System.out
        errorOutput = System.err
    }

    @Override
    void exec() {
//        jvmArgs '-Xdebug', '-Xrunjdwp:transport=dt_socket,server=y,suspend=y,address=5005'
        def args = []
        args << projectDir.get().toString()
        args << distCache.get().toString()
        args << pkgName
        args << skipBootstrap

        super.setArgs(args)
        super.exec()
    }

    @Override
    JavaExec args(Object... args) {
        throw new UnsupportedOperationException()
    }

    @Override
    JavaExecSpec args(Iterable<?> args) {
        throw new UnsupportedOperationException()
    }

}

tasks.register('createDistributionCache') {
    dependsOn configurations.distributionBala
    doLast {
        configurations.distributionBala.each { artifact ->
            copy {
                from zipTree( artifact.getPath() )
                into "$buildDir/repo"
            }
        }
    }
}

ext.modName = project.projectDir.name
def ballerinaConfigFile = new File("${project.projectDir}/src/main/ballerina/Ballerina.toml")
def ballerinaConfigFileContent = ballerinaConfigFile.text
def modOrg = (ballerinaConfigFileContent =~ /org = "(.+)"/)[0][1]
def modVersion = "0.0.0"
def jarFileName = modOrg + "-" + project.ext.modName + "-" + modVersion

tasks.register('copyBallerinaProject', Copy) {
    from project.projectDir.toString() + "/src/main/ballerina/"
    into project.buildDir.toString() + "/ballerina-src"
}

tasks.register('ballerinaLangLibBuild', BallerinaLangLibBuildTask) {
    classpath = files(project.configurations.balaCreat, jar.archiveFile)
    outputJarFileName = jarFileName
    dependsOn copyBallerinaProject
    dependsOn createDistributionCache
    dependsOn jar
}

artifacts {
    distributionBala file("$buildDir/ballerina-src/target/" + modName + ".zip"), {
        builtBy(ballerinaLangLibBuild)
    }
    distributionBirJar file("$buildDir/ballerina-src/target/${jarFileName}.jar"), {
        builtBy(ballerinaLangLibBuild)
    }
    balDoc file("$buildDir/ballerina-src/target/apidocs/"), {
        builtBy(ballerinaLangLibBuild)
    }
}
