/*
 * Copyright (c) 2019, WSO2 Inc. (http://www.wso2.org) All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */
 
apply from: "$rootDir/gradle/javaProjectWithExtBalo.gradle"

configurations {
    dist {
      transitive false
    }
    dependency {
        transitive false
    }
}

dependencies {
    implementation project(':ballerina-core')
    implementation project(':ballerina-lang')
    implementation project(':ballerina-builtin')
    implementation project(':language-server:language-server-compiler')
    implementation project(':ballerina-http')
    implementation project(':ballerina-openapi')
    implementation project(':openapi-ballerina:openapi-to-ballerina-generator')
    implementation project(':openapi-ballerina:ballerina-to-openapi-generator')
    implementation project(':ballerina-h2')
    implementation project(':ballerina-mysql')
    implementation project(':ballerina-sql')
    implementation project(':ballerina-transactions')
    implementation project(':ballerina-websub')
    implementation project(':ballerina-jms')
    implementation project(':ballerina-grpc')
    implementation project(':ballerina-filepath')
    implementation project(':testerina:testerina-core')
    implementation project(':ballerina-activemq-artemis')
    implementation project(':ballerina-rabbitmq')
    implementation project(':ballerina-nats')
    implementation project(':ballerina-launcher')

    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml'
    implementation 'io.netty:netty-buffer'
    implementation 'org.slf4j:slf4j-jdk14'
    implementation 'com.google.code.findbugs:jsr305'
    implementation 'org.eclipse.lsp4j:org.eclipse.lsp4j'
    implementation 'com.h2database:h2'
    implementation 'org.eclipse.lsp4j:org.eclipse.lsp4j.jsonrpc'
    implementation 'org.apache.commons:commons-lang3'
    implementation 'org.wso2.orbit.org.antlr:antlr4-runtime'
    implementation 'commons-io:commons-io'

    baloImplementation project(path: ':ballerina-reflect', configuration: 'baloImplementation')
    baloImplementation project(path: ':ballerina-math', configuration: 'baloImplementation')
    baloImplementation project(path: ':ballerina-encoding', configuration: 'baloImplementation')
    baloImplementation project(path: ':ballerina-task', configuration: 'baloImplementation')
    baloImplementation project(path: ':ballerina-time', configuration: 'baloImplementation')
    baloImplementation project(path: ':ballerina-mime', configuration: 'baloImplementation')
    baloImplementation project(path: ':ballerina-log-api', configuration: 'baloImplementation')
    baloImplementation project(path: ':ballerina-io', configuration: 'baloImplementation')
    baloImplementation project(path: ':ballerina-file', configuration: 'baloImplementation')
    baloImplementation project(path: ':ballerina-config-api', configuration: 'baloImplementation')
    baloImplementation project(path: ':ballerina-crypto', configuration: 'baloImplementation')
    baloImplementation project(path: ':ballerina-cache', configuration: 'baloImplementation')
    baloImplementation project(path: ':ballerina-auth', configuration: 'baloImplementation')
    baloImplementation project(path: ':ballerina-http', configuration: 'baloImplementation')
    baloImplementation project(path: ':ballerina-openapi', configuration: 'baloImplementation')
    baloImplementation project(path: ':ballerina-h2', configuration: 'baloImplementation')
    baloImplementation project(path: ':ballerina-mysql', configuration: 'baloImplementation')
    baloImplementation project(path: ':ballerina-sql', configuration: 'baloImplementation')
    baloImplementation project(path: ':ballerina-transactions', configuration: 'baloImplementation')
    baloImplementation project(path: ':ballerina-websub', configuration: 'baloImplementation')
    baloImplementation project(path: ':ballerina-jms', configuration: 'baloImplementation')
    baloImplementation project(path: ':ballerina-grpc', configuration: 'baloImplementation')
    baloImplementation project(path: ':ballerina-runtime-api', configuration: 'baloImplementation')
    baloImplementation project(path: ':ballerina-internal', configuration: 'baloImplementation')
    baloImplementation project(path: ':ballerina-system', configuration: 'baloImplementation')
    baloImplementation project(path: ':ballerina-builtin', configuration: 'baloImplementation')
    baloImplementation project(path: ':ballerina-filepath', configuration: 'baloImplementation')
    baloImplementation project(path: ':ballerina-nats', configuration: 'baloImplementation')
    baloImplementation project(path: ':ballerina-activemq-artemis', configuration: 'baloImplementation')
    baloImplementation project(path: ':ballerina-utils', configuration: 'baloImplementation')
    baloImplementation project(path: ':ballerina-rabbitmq', configuration: 'baloImplementation')
    baloImplementation project(path: ':testerina:testerina-core', configuration: 'baloImplementation')

    dependency 'com.h2database:h2:1.4.199'
    dependency 'org.eclipse.lsp4j:org.eclipse.lsp4j:0.6.0'
    dependency 'org.eclipse.lsp4j:org.eclipse.lsp4j.jsonrpc:0.6.0'
    dependency 'commons-io:commons-io:2.6'
    
    testCompile 'org.testng:testng'
}

description = 'Ballerina - Language server - Core'

CopySpec copyJarSpec = copySpec {
    from configurations.dependency
    into("lib")
}

CopySpec copyResourceSpec = copySpec {
    from file("$buildDir/lib/tools/lang-server/resources/lang-server-index.sql")
    into("resources")
}

// Fix for windows class path too long issue
task pathingJar(type: Jar) {
    appendix = "pathing"
    doFirst {
        manifest {
            attributes "Class-Path": sourceSets.main.runtimeClasspath.files.collect {
                it.toURL().toString().replaceFirst("file:/", "/")
            }.join(' ')
        }
    }
}

task clearPathingJar(type: Delete) {
    delete pathingJar.archiveFile
}

task generateIndex(type: JavaExec) {
    dependsOn pathingJar
    dependsOn createBalHome
    outputs.dir("$buildDir/lib/tools/lang-server/resources")
    outputs.cacheIf { true }
    systemProperty("ballerina.home", "$buildDir")
    classpath = files(pathingJar.archivePath) + sourceSets.main.output
    main = 'org.ballerinalang.langserver.index.tools.IndexGenerator'
    finalizedBy 'clearPathingJar'
    // Uncomment the following to run the index generator in the debug mode while build
    // debug = true
}

task createZip(type: Zip) {
    dependsOn createBalHome
    dependsOn generateIndex
    with copyJarSpec
    with copyResourceSpec
}

artifacts.add('dist', createZip.archiveFile) {
    builtBy createZip
}

test {
    systemProperty "ballerina.home", "$buildDir"
    systemProperty "experimental", "true"
    useTestNG() {
        suites 'src/test/resources/testng.xml'
    }
}

createJavadoc {
    // TODO: remove following, it fails at org.eclipse currently
    exclude "**"
}
