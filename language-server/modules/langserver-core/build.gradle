/*
 * Copyright (c) 2019, WSO2 Inc. (http://www.wso2.org) All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

apply from: "$rootDir/gradle/javaProjectWithExtBalo.gradle"
apply from: "$rootDir/gradle/baseNativeStdLibProject.gradle"

configurations.all {
    resolutionStrategy {
        preferProjectModules()
    }
}

configurations {
    dist {
        transitive false
    }
    dependency {
        transitive false
    }
    libs
}

dependencies {
    implementation project(':ballerina-lang')
    implementation project(':ballerina-tools-api')
    implementation project(':ballerina-parser')
    implementation project(':ballerina-runtime')
    implementation project(':ballerina-parser')
    implementation project(':language-server:language-server-commons')
    implementation project(':language-server:language-server-compiler')
    implementation project(':openapi-ballerina:openapi-validator')
    implementation project(':openapi-ballerina:openapi-to-ballerina-generator')
    implementation project(':openapi-ballerina:ballerina-to-openapi-generator')

    // Standard libs
    implementation project(':ballerina-lang:annotations')
    implementation project(':language-server:language-server-stdlib')

    implementation project(':ballerina-tool')

    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml'
    implementation 'io.netty:netty-buffer'
    implementation 'org.slf4j:slf4j-jdk14'
    implementation 'com.google.code.findbugs:jsr305'
    implementation 'org.eclipse.lsp4j:org.eclipse.lsp4j'
    implementation 'com.h2database:h2'
    implementation 'org.eclipse.lsp4j:org.eclipse.lsp4j.jsonrpc'
    implementation 'org.apache.commons:commons-lang3'
    implementation 'org.wso2.orbit.org.antlr:antlr4-runtime'
    implementation 'commons-io:commons-io'
    implementation 'io.swagger:swagger-core'
    implementation 'io.swagger:swagger-models'
    implementation 'io.swagger:swagger-parser'
    implementation 'io.swagger.core.v3:swagger-core'
    implementation 'io.swagger.core.v3:swagger-models'
    implementation 'io.swagger.parser.v3:swagger-parser'
    implementation 'io.swagger.parser.v3:swagger-parser-core'
    implementation 'io.swagger.parser.v3:swagger-parser-v2-converter'
    implementation 'io.swagger.parser.v3:swagger-parser-v3'
    implementation 'com.github.jknack:handlebars:4.0.6'
    implementation 'com.moandjiezana.toml:toml4j'

    baloImplementation project(path: ':ballerina-lang:annotations', configuration: 'baloImplementation')
    baloImplementation project(path: ':language-server:language-server-stdlib', configuration: 'baloImplementation')

    dependency 'com.h2database:h2:1.4.199'
    dependency 'org.eclipse.lsp4j:org.eclipse.lsp4j:0.8.1'
    dependency 'org.eclipse.lsp4j:org.eclipse.lsp4j.jsonrpc:0.8.1'
    dependency 'commons-io:commons-io:2.6'
    dependency 'io.swagger:swagger-core:2.0.0-rc0'
    dependency 'io.swagger:swagger-models:2.0.0-rc0'
    dependency 'io.swagger:swagger-parser:2.0.0-rc1'
    dependency 'io.swagger.core.v3:swagger-core:2.1.1'
    dependency 'io.swagger.core.v3:swagger-models:2.1.1'
    dependency 'io.swagger.parser.v3:swagger-parser:2.0.17'
    dependency 'io.swagger.parser.v3:swagger-parser-core:2.0.17'
    dependency 'io.swagger.parser.v3:swagger-parser-v2-converter:2.0.17'
    dependency 'io.swagger.parser.v3:swagger-parser-v3:2.0.17'

    testCompile 'org.testng:testng'
}

description = 'Ballerina - Language server - Core'

// Fix for windows class path too long issue
task pathingJar(type: Jar) {
    appendix = "pathing"
    doFirst {
        manifest {
            attributes "Class-Path": sourceSets.main.runtimeClasspath.files.collect {
                it.toURL().toString().replaceFirst("file:/", "/")
            }.join(' ')
        }
    }
}

task clearPathingJar(type: Delete) {
    delete pathingJar.archiveFile
}

task createZip(type: Zip) {
    dependsOn createBalHome
}

artifacts {
    dist file: file(createZip.archiveFile), builtBy: createZip
    libs configurations.dependency.files
}

createJavadoc {
    // TODO: remove following, it fails at org.eclipse currently
    exclude "**"
}

test {
    systemProperty "ballerina.home", "$buildDir/ballerina-home/main/"
    systemProperty "experimental", "true"
    systemProperty "ballerina.version", project.version
    useTestNG() {
        suites 'src/test/resources/testng.xml'
    }
}

task buildAndCopy(type: Copy) {
    dependsOn build
    from 'build/libs/language-server-core-' + project.version + '.jar'
    into  project(':jballerina-tools').projectDir.absolutePath +
            '/build/extracted-distributions/jballerina-tools-' + project.version +
            '/bre/lib';
}
