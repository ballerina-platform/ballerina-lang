/*
 * Copyright (c) 2020, WSO2 Inc. (http://www.wso2.org) All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */
apply plugin: 'base'
apply from: "$rootDir/gradle/javaProject.gradle"

description = 'Ballerina - Testerina Test'

def extractedDist = "$buildDir/extractedDistribution/jballerina-tools-${project.version}"

configurations {
    jballerinaTools
    compilerPluginJar {
        transitive false
    }
}

dependencies {
    jballerinaTools project(path: ':jballerina-tools', configuration: 'zip')
    testImplementation 'org.testng:testng'
    testImplementation 'org.slf4j:slf4j-jdk14'
    testImplementation "com.google.code.gson:gson:${project.gsonVersion}"
    implementation 'info.picocli:picocli'

    implementation project(path: ':ballerina-test-utils')
    implementation project(':ballerina-runtime')

    implementation "org.jacoco:org.jacoco.core:${project.jacocoVersion}"
    implementation "org.jacoco:org.jacoco.report:${project.jacocoVersion}"
    implementation "org.ow2.asm:asm:${project.ow2AsmVersion}"
    implementation "org.ow2.asm:asm-commons:${project.ow2AsmCommonsVersion}"
    implementation "org.ow2.asm:asm-tree:${project.ow2AsmTreeVersion}"
    implementation "org.testng:testng:${project.testngVersion}"

    implementation group: 'org.jacoco', name: 'org.jacoco.core', version: "${project.jacocoVersion}"
    implementation group: 'org.jacoco', name: 'org.jacoco.report', version: "${project.jacocoVersion}"
    implementation group: 'org.ow2.asm', name: 'asm', version: "${project.ow2AsmVersion}"
    implementation group: 'org.ow2.asm', name: 'asm-commons', version: "${project.ow2AsmCommonsVersion}"
    implementation group: 'org.ow2.asm', name: 'asm-tree', version: "${project.ow2AsmTreeVersion}"
    implementation group: 'io.github.java-diff-utils', name: 'java-diff-utils', version: "${project.javaDiffUtilsVersion}"

    compilerPluginJar project(':project-api-test-artifact:init-function-code-modify-compiler-plugin')
    compilerPluginJar project(':project-api-test-artifact:remove-function-code-modify-compiler-plugin')
    compilerPluginJar project(':project-api-test-artifact:add-remove-function-code-modify-compiler-plugin')
    compilerPluginJar project(':project-api-test-artifact:diagnostic-utils-lib')
}

jacoco {
    toolVersion = "${project.jacocoVersion}"
}

task copyCompilerPluginJars(type: Copy) {
    from configurations.compilerPluginJar
    into "$buildDir/compiler-plugin-jars"
}

task extractDistribution(type: Copy) {
    dependsOn ':jballerina-tools:build'
    from zipTree(configurations.jballerinaTools.asPath)
    into "$buildDir/extractedDistribution"

}

task testUtilsJar(type: Jar) {
    from sourceSets.test.runtimeClasspath
    include "org/ballerinalang/testerina/utils/**"
    archiveFileName = "interops.jar"
}

// Integration tests for testerina
test {
    mustRunAfter ':jballerina-integration-test:test'
    dependsOn extractDistribution
    dependsOn testUtilsJar
    dependsOn copyCompilerPluginJars
    systemProperty 'enableTesterinaTests', 'true'

    maxParallelForks = 1
    systemProperty 'basedir', "$buildDir"
    systemProperty 'libdir', "$buildDir"
    systemProperty 'server.zip', configurations.jballerinaTools.asPath
    systemProperty 'jballerina.server.zip', configurations.jballerinaTools.asPath
    systemProperty 'java.util.logging.config.file', "$buildDir/src/test/resources/logging.properties"

    useTestNG() {
        suites 'src/test/resources/testng.xml'
    }

    jacoco.excludes = ["org.terasology.protobuf.*", "*MethodAccess", "*FieldAccess"]
}
