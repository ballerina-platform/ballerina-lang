/*
 * Copyright (c) 2020, WSO2 Inc. (http://www.wso2.org) All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

plugins {
    id 'base'
    id 'javaProject'
}

description = 'Ballerina - Debugger Test'

def extractedDist = "$buildDir/extractedDistribution/jballerina-tools-${project.version}"

configurations {
    jballerinaTools
}

dependencies {
    jballerinaTools project(path: ':jballerina-tools', configuration: 'zip')
    implementation group: 'org.eclipse.lsp4j', name: 'org.eclipse.lsp4j.debug', version: '0.12.0'
    implementation libs.testng
    implementation libs.slf4j.simple
    implementation libs.apache.commons.lang3
    implementation libs.picocli
    implementation project(path: ':ballerina-test-utils')
}

tasks.register('extractDistribution', Copy) {
    dependsOn ':jballerina-tools:build'
    from zipTree(configurations.jballerinaTools.asPath)
    into "$buildDir/extractedDistribution"

}

// Integration tests for jBallerina debugger
test {
    mustRunAfter ':jballerina-integration-test:test'
    dependsOn extractDistribution

    maxParallelForks = 1
    systemProperty 'basedir', "$buildDir"
    systemProperty 'libdir', "$buildDir"
    systemProperty 'server.zip', configurations.jballerinaTools.asPath
    systemProperty 'jballerina.server.zip', configurations.jballerinaTools.asPath
    systemProperty 'java.util.logging.config.file', "$buildDir/src/test/resources/logging.properties"

    useTestNG() {
        suites 'src/test/resources/testng.xml'
    }
}
