/*
 * Copyright (c) 2019, WSO2 Inc. (http://www.wso2.org) All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */
 
apply from: "$rootDir/gradle/javaProject.gradle"

configurations {
    ballerinaTools
    jballerinaTools
    testUtilsJar {
        transitive false
    }
    testUtilsBal
    ballerina
}

dependencies {
    implementation project(':ballerina-core')
    implementation project(':ballerina-lang')
    implementation project(':ballerina-packerina')
    implementation project(':ballerina-tool')   
    implementation project(path: ':ballerina-test-utils', configuration: 'shadow')
    implementation 'org.slf4j:slf4j-jdk14'

    testCompile 'org.awaitility:awaitility'
    testCompile 'commons-io:commons-io'
    testCompile 'org.testng:testng'
    ballerinaTools project(path: ':ballerina-tools', configuration: 'zip')
    jballerinaTools project(path: ':jballerina-tools', configuration: 'zip')
    ballerina project(path: ':ballerina', configuration: 'resourceFiles')
    testUtilsJar project(path: ':ballerina-test-utils', configuration: 'shadow')
    testUtilsBal project(path: ':ballerina-test-utils', configuration: 'shadow')
}

test {
    maxParallelForks = 1
    forkEvery=500
    // make sure ballerina tools zip is built before test run
    dependsOn = [":ballerina-tools:build", ":jballerina-tools:build", ":jballerina-integration-test:test", 
                 // ":examples-test:test"
                 ]

    systemProperty 'project.build.directory', "$buildDir"
    systemProperty 'server.zip', configurations.ballerinaTools.asPath
    systemProperty 'jballerina.server.zip', configurations.jballerinaTools.asPath
    systemProperty 'java.util.logging.config.file', "$buildDir/resources/test/logging.properties"
    systemProperty 'java.util.logging.manager', 'org.ballerinalang.logging.BLogManager'
    systemProperty 'integration.test.utils.service.file', configurations.testUtilsBal.asPath
    systemProperty 'javax.net.ssl.trustStore', "${configurations.ballerina.asPath}/security/ballerinaTruststore.p12"
    systemProperty 'javax.net.ssl.trustStorePassword', 'ballerina'
    systemProperty 'ballerina.agent.path', configurations.testUtilsJar.asPath
    useTestNG() {
        suites 'src/test/resources/testng.xml'
    }
}

configurations {
    testCompile.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    testCompile.exclude group: 'org.slf4j', module: 'slf4j-simple'
}

description = 'Ballerina - Tools Integration Test'
