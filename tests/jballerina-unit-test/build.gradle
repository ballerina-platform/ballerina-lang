/*
 * Copyright (c) 2019, WSO2 Inc. (http://www.wso2.org) All Rights Reserved.
 *
 * WSO2 Inc. licenses this file to you under the Apache License,
 * Version 2.0 (the "License"); you may not use this file except
 * in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations
 * under the License.
 *
 */

apply from: "$rootDir/gradle/javaProject.gradle"
apply from: "$rootDir/gradle/ballerinaLangLibLoad.gradle"

dependencies {
    implementation 'org.slf4j:slf4j-api'
    implementation project(':ballerina-lang')
    implementation project(':ballerina-lang:jballerina.java')
    implementation project(':ballerina-lang-test')
    implementation project(':ballerina-runtime')
    implementation project(':ballerina-lang:value');
    implementation project(':ballerina-lang:regexp');

    testImplementation "org.mockito:mockito-core:${project.mockitoCoreVersion}"
    testImplementation "org.mockito:mockito-testng:${project.mockitoTestNGVersion}"
    testImplementation 'org.wso2.transport.http:org.wso2.transport.http.netty'
    testImplementation 'org.wso2.carbon.messaging:org.wso2.carbon.messaging'
    testImplementation 'org.testng:testng'
    testImplementation project(path: ':ballerina-test-utils', configuration: 'shadow')
    testImplementation project(':ballerina-runtime')
    testRuntimeOnly project(':ballerina-runtime')

    distributionBala project(path: ':testerina:testerina-core', configuration: 'distributionBala')
}

description = 'JBallerina - Unit Test Module'

test {
    systemProperty "java.util.logging.config.file", "src/test/resources/logging.properties"
    systemProperty "enableJBallerinaTests", "true"

    jvmArgs = ['-Xms512m', '-Xmx3g', '--add-opens=java.base/java.util=ALL-UNNAMED',
               '--add-opens=java.base/java.lang=ALL-UNNAMED']

    useTestNG() {
        suites 'src/test/resources/testng.xml'
    }

    afterSuite { suite, result ->
        result.exception?.printStackTrace()
    }

    testLogging {
        // this will only change --warn flag behaviour
        warn {
            events = ["failed", "skipped"]
            exceptionFormat "full"
        }
    }
}

configurations {
    testImplementation.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    testImplementation.exclude group: 'org.slf4j', module: 'slf4j-simple'
    testImplementation.exclude group: 'org.ops4j.pax.logging', module: 'pax-logging-api'
}

configurations.all {
    resolutionStrategy.preferProjectModules()
}
tasks.compileTestJava.dependsOn(':ballerina-lang-test:copyInteropImports')
