/*
 * Copyright (c) 2020, WSO2 Inc. (http://www.wso2.org) All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

apply from: "$rootDir/gradle/javaProject.gradle"

configurations.all {
    resolutionStrategy {
        preferProjectModules()
    }
}

dependencies {
    implementation project(':ballerina-tools-api')
    implementation project(':ballerina-parser')
    implementation project(':ballerina-lang')
    implementation "org.apache.commons:commons-lang3:${project.apacheCommonsLang3Version}"
    implementation project(':toml-parser')

    testImplementation 'com.google.code.gson:gson'
    testImplementation 'org.testng:testng'
}

description = 'Ballerina - Formatter Core'

test {
    useTestNG() {
        suites 'src/test/resources/testng.xml'
    }
}

ext.moduleName = 'ballerina.metrics'

compileJava {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
        ]
        classpath = files()
    }
}

task parserTests(type: Copy) {
    from "../../../../compiler/ballerina-parser/src/test/resources"
    into "$buildDir/resources/test/parser-tests"
    include "**/*.bal"
}

tasks.test.dependsOn("parserTests")
tasks.checkstyleTest.dependsOn(":formatter:formatter-core:parserTests")
