/*
 *  Copyright (c) 2021, WSO2 Inc. (http://www.wso2.org) All Rights Reserved.
 *
 *  WSO2 Inc. licenses this file to you under the Apache License,
 *  Version 2.0 (the "License"); you may not use this file except
 *  in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing,
 *  software distributed under the License is distributed on an
 *  "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 *  KIND, either express or implied.  See the License for the
 *  specific language governing permissions and limitations
 *  under the License.
 */

plugins {
    id 'javaProject'
    id 'ballerinaLangLibLoad'
}

configurations {
    compile.transitive = false
    compileClasspath.extendsFrom(compileOnly)
}

dependencies {
    compileOnly project(':language-server:language-server-commons')
    compileOnly project(':central-client')
    compileOnly project(':ballerina-lang')
    compileOnly libs.eclipse.lsp4j
    implementation libs.gson

    testImplementation libs.testng
    testImplementation project(':language-server:language-server-commons')
    testImplementation project(':language-server:language-server-core')
    testImplementation libs.gson
    testImplementation libs.eclipse.lsp4j
}

test {
    useTestNG() {
        suites 'src/test/resources/testng.xml'
    }
    environment("BALLERINA_STAGE_CENTRAL", true)
}

description = 'LS extension for triggers'

ext.moduleName = 'trigger-service'

compileJava {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs << '--module-path' << classpath.asPath
        classpath = files()
    }
}

javadoc {
    doFirst {
        options.modulePath = classpath.toList()
        classpath = files()
    }
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes "Main-Class": "io.ballerina.trigger.BallerinaTriggerService"
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}
