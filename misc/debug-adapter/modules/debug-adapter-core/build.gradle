apply from: "$rootDir/gradle/javaProjectWithExtBalo.gradle"
apply plugin: 'com.github.johnrengelman.shadow'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.eclipse.lsp4j:org.eclipse.lsp4j.debug:0.7.1'
    implementation project(':ballerina-lang')
    implementation project(':ballerina-parser')
    implementation 'org.apache.commons:commons-compress:1.18'
    implementation files(org.gradle.internal.jvm.Jvm.current().toolsJar)
}

shadowJar {
    configurations = [project.configurations.runtimeClasspath]
    dependencies {
        exclude('META-INF/*.SF')
        exclude('META-INF/*.DSA')
        exclude('META-INF/*.RSA')
    }
}

jar {
    enabled = false
    dependsOn(shadowJar { classifier = null })
    manifest {
        attributes(
            'Main-Class': 'org.ballerinalang.debugadapter.launcher.Launcher'
        )
    }
}

createJavadoc {
    // TODO: remove following, it fails at org.eclipse currently
    exclude "**"
}

description = 'Ballerina - Debug Adapter - Debug Adaptor Core'

ext.moduleName = 'ballerina.debug.adapter.core'

compileJava {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
                '--add-exports', 'jdk.compiler/com.sun.tools.javac.util=ALL-UNNAMED'
        ]
        classpath = files()
    }
}
