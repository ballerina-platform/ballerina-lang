/*
 * Copyright (c) 2019, WSO2 Inc. (http://www.wso2.org) All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

plugins {
    id 'javaProject'
    id 'ballerinaLangLibLoad'
}

configurations.all {
    resolutionStrategy.preferProjectModules()
}

configurations {
    distBal {
        transitive false
    }
    distributionBala
    distributionBirJar
    balRt
    testRt
    testCore
}

dependencies {
    implementation project(':ballerina-tools-api')
    implementation project(':ballerina-parser')
    implementation project(':ballerina-lang')
    implementation project(':ballerina-lang:annotations')
    implementation libs.commons.io
    implementation libs.squareup.okhttp
    implementation libs.gson

    distBal project(path: ':ballerina-lang:annotations', configuration: 'balaImplementation')

    testImplementation libs.testng
    testImplementation libs.slf4j.jdk14

    balRt project(':ballerina-rt')
}


tasks.register('createTestDistributionCache', Copy) {
    dependsOn configurations.distributionBala
    from configurations.distributionBala
    into "$buildDir/repo"
}

tasks.register('createTestBre', Copy) {
    from configurations.balRt
    from configurations.testRt
    from configurations.testCore
    from configurations.distributionBirJar
    into "$buildDir/bre/lib"
}

test {
    dependsOn ':ballerina-lang:build'
    dependsOn createTestDistributionCache
    dependsOn createTestBre

    systemProperty "ballerina.home", "$buildDir"

    testLogging {
        events "failed"
        exceptionFormat "full"
        showStackTraces = true
        showStandardStreams = true
    }

    useTestNG() {
        suites 'src/test/resources/testng.xml'
    }
}

artifacts {
    file("$sourceSets.main.output.resourcesDir/doc-ui/")
}

processResources {
    include '**/properties'
    include 'doc-ui/index.html'
    include 'builtin-types-descriptions/'
    include 'keywords-descriptions/'
    include 'release-description.md'
    filter { String line -> line.replace('${short.version}', "${project.version}")}
}

tasks.register('copyBala', Copy) {
    from configurations.distBal
    into("$buildDir/lib")
}

description = 'Ballerina - Docerina'

ext.moduleName = 'docerina'
