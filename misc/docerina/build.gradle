/*
 * Copyright (c) 2019, WSO2 Inc. (http://www.wso2.org) All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */
 
apply plugin: 'base'
apply plugin: 'com.github.johnrengelman.shadow'

apply from: "$rootDir/gradle/javaProject.gradle"
apply from: "$rootDir/gradle/ballerinaLangLibLoad.gradle"

configurations.all {
    resolutionStrategy.preferProjectModules()
}

configurations {
    distBal {
        transitive false
    }
    distributionBala
    distributionBirJar
    balRt
    testRt
    testCore
}

dependencies {
    implementation project(':ballerina-tools-api')
    implementation project(':ballerina-parser')
    implementation project(':ballerina-lang')
    implementation project(':ballerina-lang:annotations')
    implementation project(':ballerina-config')
    implementation 'info.picocli:picocli'
    implementation 'commons-io:commons-io'
    implementation 'com.google.code.gson:gson:2.7'
    implementation 'org.slf4j:slf4j-api'
    implementation 'org.apache.commons:commons-lang3'

    distBal project(path: ':ballerina-lang:annotations', configuration: 'balaImplementation')

    testImplementation 'org.testng:testng'
    testImplementation 'org.slf4j:slf4j-jdk14'

    balRt project(':ballerina-rt')
}


task createTestDistributionCache(type: Copy) {
    dependsOn configurations.distributionBala
    from configurations.distributionBala
    into "$buildDir/repo"
}

task createTestBre(type: Copy) {
    from configurations.balRt
    from configurations.testRt
    from configurations.testCore
    from configurations.distributionBirJar
    into "$buildDir/bre/lib"
}

test {
    dependsOn ':ballerina-lang:build'
    dependsOn createTestDistributionCache
    dependsOn createTestBre

    systemProperty "ballerina.home", "$buildDir"

    testLogging {
        events "failed"
        exceptionFormat "full"
        showStackTraces = true
        showStandardStreams = true
    }

    useTestNG() {
        suites 'src/test/resources/testng.xml'
    }
}

artifacts {
    file("$sourceSets.main.output.resourcesDir/doc-ui/")
}

jar {
    enabled = false
    dependsOn(shadowJar { classifier = null })
    manifest {
        attributes(
            'Main-Class': 'org.ballerinalang.docgen.docs.BallerinaDocGeneratorMain',
            'Implementation-Title': 'Ballerina - Docerina',
            'Specification-Title': 'Ballerina - Docerina',
            'Specification-Vendor': 'WSO2',
            'Implementation-Vendor-Id': 'org.ballerinalang',
            'Implementation-Vendor': 'WSO2',
            'Implementation-URL': 'https://ballerina.io/misc/docerina/'
        )
    }
}

processResources {
    include '**/properties'
    include 'doc-ui/index.html'
    include 'builtin-types-descriptions/'
    include 'keywords-descriptions/'
    include 'release-description.md'
    filter { String line -> line.replace('${short.version}', "${project.shortVersion}")}
}

shadowJar {
    configurations = [project.configurations.runtimeClasspath]
    dependencies {
        include(dependency('com.atlassian.commonmark:commonmark'))
        include(dependency('com.atlassian.commonmark:commonmark-ext-gfm-tables'))
        include(dependency(':ballerina-lang'))
        include(dependency(':ballerina-parser'))
        exclude('META-INF/*.SF')
        exclude('META-INF/*.DSA')
        exclude('META-INF/*.RSA')
    }
}

task copyBala(type: Copy) {
    from configurations.distBal
    into("$buildDir/lib")
}

description = 'Ballerina - Docerina'

ext.moduleName = 'docerina'

compileJava {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
        ]
        classpath = files()
    }
}
