/*
 * Copyright (c) 2017, WSO2 Inc. (http://www.wso2.org) All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

plugins {
    id "org.jetbrains.intellij" version "0.3.7"
    id "de.undercouch.download" version "3.2.0"
    id "org.sonarqube" version "2.6"
}

version = ballerinaPluginVersion

repositories {
    mavenCentral()
    maven { url 'http://dl.bintray.com/jetbrains/intellij-plugin-service' }
    maven { url 'https://jitpack.io' }
}

configurations {
    composerDistribution {
        transitive false
    }
    composerFonts {
        transitive false
    }
}

// LSP Dependencies
dependencies {
    compile group: 'org.eclipse.lsp4j', name: 'org.eclipse.lsp4j', version: '0.6.0'
    compile group: 'com.vladsch.flexmark', name: 'flexmark', version: '0.34.58'
    compile 'com.github.ballerina-platform:lsp4intellij:master-SNAPSHOT'
    compile group: 'com.github.jknack', name: 'handlebars', version: '4.1.2'
    composerDistribution project(path: ':composer-library', configuration: 'composer')
    composerFonts project(path: ':composer-library', configuration: 'fonts')
    configurations.compile.exclude(group: 'org.slf4j', module: 'slf4j-api')
}

apply plugin: 'java'
sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', 'src/main/gen']
        }
    }
}

apply plugin: 'org.jetbrains.intellij'
intellij {
    // For a full list of IntelliJ IDEA releases, please see https://www.jetbrains.com/intellij-repository/releases.

    version ideaVersion
    pluginName ballerinaPluginName
    updateSinceUntilBuild false
    // plugins 'coverage' //Bundled plugin dependencies
    // Uncomment this line to test the plugin using small IDEs. Add -Dorg.gradle.project.localIdePath="IDE_HOME_PATH"
    // VM option in run config.
    // alternativeIdePath localIdePath
}

apply plugin: 'de.undercouch.download'
task downloadPsiViewerPlugin() {
    download {
        src 'https://plugins.jetbrains.com/plugin/download?updateId=31087'
        dest new File("${buildDir}/tmp/plugins/", 'PsiViewer.jar')
        onlyIfNewer true
    }
}

apply plugin: "jacoco"
jacoco {
    toolVersion = "0.8.1"
    reportsDir = file("$buildDir/customJacocoReportDir")
}

task copyPsiViewerPluginToSandBox(type: Copy) {
    from "${buildDir}/tmp/plugins/PsiViewer.jar"
    into "${buildDir}/idea-sandbox/plugins/"
}

task assemble(overwrite: true) {
    dependsOn ':composer-library:build'
    dependsOn 'copyComposerDistribution'
}

task copyComposerDistribution() {
    dependsOn(':composer-library:assemble')
    doFirst {
        copy {
            from file(configurations.composerDistribution.asPath)
            exclude '**/*.map'
            exclude '**/*.d.ts'
            into "resources/composer"
        }
        copy {
            from file(configurations.composerFonts.asPath)
            into "resources/composer/font"
        }
    }
}

//copyPsiViewerPluginToSandBox.dependsOn downloadPsiViewerPlugin
copyPsiViewerPluginToSandBox.mustRunAfter prepareSandbox
runIde.dependsOn copyPsiViewerPluginToSandBox

apply plugin: 'checkstyle'
checkstyle {
    configDir = file("${rootDir}/src/main/resources/checkstyle")
    checkstyleMain {
        source "src/main/java"
        include '**/*.java'
    }
    checkstyleTest {
        source "src/test/java"
    }
}

//runIde.dependsOn check
//buildPlugin.dependsOn check
