/*
 * Copyright (c) 2021, WSO2 Inc. (http://www.wso2.org) All Rights Reserved.
 *
 * WSO2 Inc. licenses this file to you under the Apache License,
 * Version 2.0 (the "License"); you may not use this file except
 * in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

apply from: "$rootDir/gradle/javaProject.gradle"
apply from: "$rootDir/gradle/ballerinaLangLibLoad.gradle"

description = 'Ballerina - Ballerina Shell CLI'

group = 'io.ballerina'
version = '0.0.1-SNAPSHOT'

dependencies {
    implementation project(":ballerina-shell:shell-core")
    implementation('org.jline:jline:3.11.0')

    testImplementation('org.testng:testng')
    implementation 'com.google.code.gson:gson:2.8.7'
}

compileJava {
    doFirst {
        options.compilerArgs += [
                "-Aproject=${project.group}/${project.name}",
                '--module-path', classpath.asPath,
        ]
        classpath = files()
    }
}

test {
    useTestNG() {
        suites 'src/test/resources/testng.xml'
    }
}

def mainCliClass = 'io.ballerina.shell.cli.ReplShellApplication'
task run(type: JavaExec) {
    main = mainCliClass
    standardInput = System.in
    classpath = sourceSets.main.runtimeClasspath
}

task fatJar(type: Jar) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest { attributes 'Main-Class': mainCliClass }
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

ext.moduleName = 'io.ballerina.shell.cli'
