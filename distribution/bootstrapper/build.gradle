/*
 * Copyright (c) 2019, WSO2 Inc. (http://www.wso2.org) All Rights Reserved.
 *
 * WSO2 Inc. licenses this file to you under the Apache License,
 * Version 2.0 (the "License"); you may not use this file except
 * in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations
 * under the License.
 *
 */

plugins {
    id 'base'
}

//TODO: merge pack0 and pack1

configurations {
    pack0 { transitive false } // clean pack from previous build
    pack1 { transitive false } // pack with build cmd
    pack2 { transitive false } // pack with stdlib/bir balo
    pack2birJar { transitive false }
    pack2native { transitive false }
    pack2bir { transitive false }
    pack3 { transitive false } // pack with stdlib/jvm balo
    pack3birJar { transitive false }
    pack3native { transitive false }
}

apply from: "$rootDir/gradle/javaProject.gradle"

dependencies {
    pack0 group: 'org.ballerinalang', name: 'jballerina-tools', version: project.bootstrappedOn, ext: 'zip'

    pack1 group: 'org.ballerinalang', name: 'ballerina-packerina', version: project.bootstrappedOn
    pack1 'org.ow2.asm:asm:6.2.1'

    pack2bir project(path: ':ballerina-bir-old', configuration: 'oldBir')
    pack2bir project(path: ':ballerina-bir-old', configuration: 'nativeMap')
    pack2bir project(path: ':ballerina-jvm-old', configuration: 'oldBir')
    pack2bir project(path: ':ballerina-jvm-old', configuration: 'nativeMap')

    pack3birJar project(path: ':ballerina-backend-jvm-old', configuration: 'oldBirJar')
    pack3native project(path: ':ballerina-backend-jvm-old', configuration: 'default')
    pack3native project(path: ':ballerina-bir-old', configuration: 'default')
    pack3birJar project(path: ':ballerina-bir-old', configuration: 'oldBirJar')
    pack3native project(path: ':ballerina-jvm-old', configuration: 'default')
    pack3birJar project(path: ':ballerina-jvm-old', configuration: 'oldBirJar')
}


//def bootBalHome = distDir + "/ballerina-" + project.bootstrappedOn // see ~/ballerina-lang/gradle.properties
def bootBalHome(packNo) {
    "${buildDir}/dist/pack" + packNo + "/jballerina-tools-" + project.bootstrappedOn
}

task unzipPack0(type: Copy) {
    dependsOn configurations.pack0
    
    fileMode = 0755
    from({ configurations.pack0.collect { zipTree(it) } }) {
        exclude "*/bin/**"
    }
    into file("${buildDir}/dist/pack0")
    outputs.dir bootBalHome(0)
    outputs.cacheIf { false }
}

task copyPack1Artifacts(type: Copy) {
    dependsOn unzipPack0
    dependsOn configurations.pack1

    from configurations.pack1

    into bootBalHome(0) + "/bre/lib"
}

task copyBalScript(type: Copy) {
    dependsOn unzipPack0

    from "src/main/resources/"
    into bootBalHome(0) + "/bin"
}

task createPack1() {
    dependsOn copyPack1Artifacts
    dependsOn copyBalScript

    outputs.dir(bootBalHome(0))
    outputs.cacheIf { false }
}

task duplicatePack1AsPack2(type: Copy) {
    dependsOn createPack1
    from bootBalHome(0)
    into bootBalHome(2)
}

task copyBirForPack2(type: Copy) {
    dependsOn duplicatePack1AsPack2
    dependsOn configurations.pack2bir

    from configurations.pack2bir
    into bootBalHome(2) + '/bir-cache/ballerina/'

    eachFile { file ->
        println file
        def moduleName = file.name.replaceFirst(~/\..+$/, '')
        file.path =  moduleName + '/0.0.0/' + file.name
    }
}

//TODO: remove this after bootstrapping m8, we no longer need the repo
task createFakeBalos(type: Zip) {

    configurations.pack2bir.findAll { it.name.endsWith(".bir") }.each { birFile ->
        def moduleName = birFile.name.replaceFirst(~/\.[^\.]+$/, '')
        task "createFakeBalo$moduleName"(type: Zip) {
//            dependsOn it
            dependsOn configurations.pack2bir

            from birFile
            into 'bir'
            archiveFileName = moduleName + ".zip"
            destinationDirectory = file(bootBalHome(2) + "/lib/repo/ballerina/$moduleName/0.0.0/")
        }
        dependsOn "createFakeBalo$moduleName"
    }
}

task copyBirJarForPack2(type: Copy) {
    dependsOn duplicatePack1AsPack2
    dependsOn configurations.pack2birJar

    from configurations.pack2birJar
    into bootBalHome(2) + "/bre/lib/"
}

task copyNativeForPack2(type: Copy) {
    dependsOn duplicatePack1AsPack2
    dependsOn configurations.pack2native

    from configurations.pack2native
    into bootBalHome(2) + "/bre/lib/"
}

task createPack2() {
    dependsOn duplicatePack1AsPack2
    dependsOn copyBirForPack2
    dependsOn createFakeBalos
}

task duplicatePack2AsPack3(type: Copy) {
    dependsOn createPack2
    from bootBalHome(2)
    exclude "**/bre/lib/compiler_backend_jvm.jar"
    exclude "**/bre/lib/ballerina-backend-jvm*.jar"
    exclude "**/bre/lib/bir.jar"
    exclude "**/bre/lib/jvm.jar"
    exclude "**/bre/lib/ballerina-bir*.jar"
    exclude "**/bre/lib/ballerina-jvm*.jar"
    into bootBalHome(3)
}

task copyBirJarForPack3(type: Copy) {
    dependsOn duplicatePack2AsPack3
    dependsOn configurations.pack3birJar

    from configurations.pack3birJar
    into bootBalHome(3) + "/bre/lib/"
}

task copyNativeForPack3(type: Copy) {
    dependsOn duplicatePack2AsPack3
    dependsOn configurations.pack3native

    from configurations.pack3native
    into bootBalHome(3) + "/bre/lib/"
}

task createPack3() {
    dependsOn copyBirJarForPack3
    dependsOn copyNativeForPack3
}

artifacts {
    pack1 file(bootBalHome(0)), { builtBy(createPack1) }
    pack2 file(bootBalHome(2)), { builtBy(createPack2) }
    pack3 file(bootBalHome(3)), { builtBy(createPack3) }
}

