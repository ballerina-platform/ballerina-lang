/*
 * Copyright (c) 2019, WSO2 Inc. (http://www.wso2.org) All Rights Reserved.
 *
 * WSO2 Inc. licenses this file to you under the Apache License,
 * Version 2.0 (the "License"); you may not use this file except
 * in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations
 * under the License.
 *
 */

plugins {
    id 'base'
}

//TODO: merge pack0 and pack1

configurations {
    pack0 { transitive false } // clean pack from previous build
    pack1 { transitive false } // pack with build cmd
    pack2 { transitive false } // pack with stdlib/bir balo
    pack2native { transitive false }
    pack3 { transitive false } // pack with stdlib/jvm balo
    pack3native { transitive false }
}

apply from: "$rootDir/gradle/javaProject.gradle"

dependencies {
    pack0 group: 'org.ballerinalang', name: 'ballerina', version: '0.992.0-m1', ext: 'zip'

    pack1 group: 'org.ballerinalang', name: 'ballerina-packerina', version: '0.992.0-m1'
    pack1 'org.ow2.asm:asm:6.2.1'

    pack2 project(path: ':ballerina-bir', configuration: 'balo')
    pack2native project(path: ':ballerina-bir', configuration: 'default')
    pack2 project(path: ':ballerina-jvm', configuration: 'balo')
    pack2native project(path: ':ballerina-jvm', configuration: 'default')

    pack3 project(path: ':ballerina-backend-jvm', configuration: 'balx')
    pack3 project(path: ':ballerina-backend-jvm', configuration: 'balo')
    pack3native project(path: ':ballerina-backend-jvm', configuration: 'default')
}


//def bootBalHome = distDir + "/ballerina-" + project.bootstrappedOn // see ~/ballerina-lang/gradle.properties
def bootBalHome(packNo) {
    "${buildDir}/dist/pack" + packNo + "/ballerina-" + project.bootstrappedOn
}

task unzipPack0(type: Copy) {
    dependsOn configurations.pack0
    
    fileMode = 0755
    from({ configurations.pack0.collect { zipTree(it) } }) {
        exclude "*/bin/**"
    }
    into file("${buildDir}/dist/pack0")
    outputs.dir bootBalHome(0)
    outputs.cacheIf { false }
}

task copyPack1Artifacts(type: Copy) {
    dependsOn unzipPack0
    dependsOn configurations.pack1

    from configurations.pack1

    into bootBalHome(0) + "/bre/lib"
    into bootBalHome(0)  + "/bre/lib"
}

task copyBalScript(type: Copy) {
    dependsOn unzipPack0

    from "src/main/resources/"
    into bootBalHome(0) + "/bin"
}

task createPack1() {
    dependsOn copyPack1Artifacts
    dependsOn copyBalScript

    outputs.dir(bootBalHome(0))
    outputs.cacheIf { false }
}

task duplicatePack1AsPack2(type: Copy) {
    dependsOn createPack1
    from bootBalHome(0)
    into bootBalHome(2)
}

task copyBalosForPack2(type: Copy) {
    dependsOn duplicatePack1AsPack2
    dependsOn configurations.pack2

    from configurations.pack2
    into bootBalHome(2) + "/lib/repo/ballerina"

    eachFile { details ->
        details.path = details.path.replace("/", "/0.0.0/")
    }
}

task copyNativeForPack2(type: Copy) {
    dependsOn duplicatePack1AsPack2
    dependsOn configurations.pack2native

    from configurations.pack2native
    into bootBalHome(2) + "/bre/lib"
}

task createPack2() {
    dependsOn copyBalosForPack2
    dependsOn copyNativeForPack2
}

task duplicatePack2AsPack3(type: Copy) {
    dependsOn createPack2
    from bootBalHome(2)
    into bootBalHome(3)
}

task copyBalxForPack3(type: Copy) {
    dependsOn duplicatePack2AsPack3
    dependsOn configurations.pack3

    from configurations.pack3 {
        include '*.balx'
    }
    into bootBalHome(3) + "/bin"
}

task copyNativeForPack3(type: Copy) {
    dependsOn duplicatePack2AsPack3
    dependsOn configurations.pack3native

    from configurations.pack3native
    into bootBalHome(3) + "/bre/lib"
}

task copyBalosForPack3(type: Copy) {
    dependsOn duplicatePack2AsPack3
    dependsOn configurations.pack3

    from configurations.pack3 {
        include '**/*.zip'
    }
    into bootBalHome(3) + "/lib/repo/ballerina"

    eachFile { details ->
        details.path = details.path.replace("/", "/0.0.0/")
    }
}

task createPack3() {
    dependsOn copyBalxForPack3
    dependsOn copyNativeForPack3
    dependsOn copyBalosForPack3
}

artifacts {
    pack1 file(bootBalHome(0)), { builtBy(createPack1) }
    pack2 file(bootBalHome(2)), { builtBy(createPack2) }
    pack3 file(bootBalHome(3)), { builtBy(createPack3) }
}

