/*
 * Copyright (c) 2019, WSO2 Inc. (http://www.wso2.org) All Rights Reserved.
 *
 * WSO2 Inc. licenses this file to you under the Apache License,
 * Version 2.0 (the "License"); you may not use this file except
 * in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations
 * under the License.
 *
 */

plugins {
    id 'base'
}

//TODO: merge pack0 and pack1

configurations {
    pack0 { transitive false } // clean pack from previous build
    pack1 { transitive false } // pack with build cmd

    pack2 { transitive false } // pack with stdlib/bir balo
    pack2birJar { transitive false }
    pack2native { transitive false }
    pack2bir { transitive false }

    pack3 { transitive false } // pack with stdlib/jvm + stdlib/bir balo
    pack3birJar { transitive false }
    pack3native { transitive false }
    pack3bir { transitive false }

    pack4 { transitive false }
    pack4birJar { transitive false }
    pack4native { transitive false }
}

apply from: "$rootDir/gradle/javaProject.gradle"

dependencies {
    pack0 group: 'org.ballerinalang', name: 'jballerina-tools', version: project.bootstrappedOn, ext: 'zip'

    pack2bir project(path: ':ballerina-bir-old', configuration: 'oldBir')
    pack2bir project(path: ':ballerina-bir-old', configuration: 'nativeMap')

    pack3bir project(path: ':ballerina-bir-old', configuration: 'oldBir')
    pack3bir project(path: ':ballerina-bir-old', configuration: 'nativeMap')
    pack3bir project(path: ':ballerina-jvm-old', configuration: 'oldBir')
    pack3bir project(path: ':ballerina-jvm-old', configuration: 'nativeMap')

    pack4birJar project(path: ':ballerina-backend-jvm-old', configuration: 'oldBirJar')
    pack4native project(path: ':ballerina-backend-jvm-old', configuration: 'default')
    pack4native project(path: ':ballerina-bir-old', configuration: 'default')
    pack4birJar project(path: ':ballerina-bir-old', configuration: 'oldBirJar')
    pack4native project(path: ':ballerina-jvm-old', configuration: 'default')
    pack4birJar project(path: ':ballerina-jvm-old', configuration: 'oldBirJar')
}


//def bootBalHome = distDir + "/ballerina-" + project.bootstrappedOn // see ~/ballerina-lang/gradle.properties
def bootBalHome(packNo) {
    "${buildDir}/dist/pack" + packNo + "/jballerina-tools-" + project.bootstrappedOn
}

task unzipPack0(type: Copy) {
    dependsOn configurations.pack0
    
    fileMode = 0755
    from({ configurations.pack0.collect { zipTree(it) } }) {
        exclude "*/bin/**"
    }
    into file("${buildDir}/dist/pack0")
    outputs.dir bootBalHome(0)
    outputs.cacheIf { false }
}

task copyPack1Artifacts(type: Copy) {
    dependsOn unzipPack0
    dependsOn configurations.pack1

    from configurations.pack1

    into bootBalHome(0) + "/bre/lib"
}

task copyBalScript(type: Copy) {
    dependsOn unzipPack0

    from "src/main/resources/"
    into bootBalHome(0) + "/bin"
}

task createPack1() {
    dependsOn copyPack1Artifacts
    dependsOn copyBalScript

    outputs.dir(bootBalHome(0))
    outputs.cacheIf { false }
}

// ------ pack 2 ------

task duplicatePack1AsPack2(type: Copy) {
    dependsOn createPack1
    from bootBalHome(0)
    into bootBalHome(2)
}

task copyBirForPack2(type: Copy) {
    dependsOn duplicatePack1AsPack2
    dependsOn configurations.pack2bir

    from configurations.pack2bir
    into bootBalHome(2) + '/bir-cache/ballerina/'

    eachFile { file ->
        println file
        def moduleName = file.name.replaceFirst(~/\..+$/, '')
        file.path =  moduleName + '/0.0.0/' + file.name
    }
}

//TODO: remove this after bootstrapping m8, we no longer need the repo
task createFakeBalos(type: Zip) {

    configurations.pack2bir.findAll { it.name.endsWith(".bir") }.each { birFile ->
        def moduleName = birFile.name.replaceFirst(~/\.[^\.]+$/, '')
        task "createFakeBalo$moduleName"(type: Zip) {
//            dependsOn it
            dependsOn configurations.pack2bir

            from birFile
            into 'bir'
            archiveFileName = moduleName + ".zip"
            destinationDirectory = file(bootBalHome(2) + "/lib/repo/ballerina/$moduleName/0.0.0/")
        }
        dependsOn "createFakeBalo$moduleName"
    }
}

task copyBirJarForPack2(type: Copy) {
    dependsOn duplicatePack1AsPack2
    dependsOn configurations.pack2birJar

    from configurations.pack2birJar
    into bootBalHome(2) + "/bre/lib/"
}

task copyNativeForPack2(type: Copy) {
    dependsOn duplicatePack1AsPack2
    dependsOn configurations.pack2native

    from configurations.pack2native
    into bootBalHome(2) + "/bre/lib/"
}

task createPack2() {
    dependsOn duplicatePack1AsPack2
    dependsOn copyBirForPack2
    dependsOn createFakeBalos
}

// ------ pack 3 ------

task duplicatePack2AsPack3(type: Copy) {
    dependsOn createPack2
    from bootBalHome(2)
    into bootBalHome(3)
}

task copyBirForPack3(type: Copy) {
    dependsOn duplicatePack2AsPack3
    dependsOn configurations.pack3bir

    from configurations.pack3bir
    into bootBalHome(3) + '/bir-cache/ballerina/'

    eachFile { file ->
        println file
        def moduleName = file.name.replaceFirst(~/\..+$/, '')
        file.path =  moduleName + '/0.0.0/' + file.name
    }
}

task copyBirJarForPack3(type: Copy) {
    dependsOn duplicatePack2AsPack3
    dependsOn configurations.pack3birJar

    from configurations.pack3birJar
    into bootBalHome(3) + "/bre/lib/"
}

task copyNativeForPack3(type: Copy) {
    dependsOn duplicatePack2AsPack3
    dependsOn configurations.pack3native

    from configurations.pack3native
    into bootBalHome(3) + "/bre/lib/"
}

task createPack3() {
    dependsOn duplicatePack2AsPack3
    dependsOn copyBirForPack3
    dependsOn createFakeBalos
}

// ------ pack 4 ------

task duplicatePack3AsPack4(type: Copy) {
    dependsOn createPack3
    from bootBalHome(3)
    exclude "**/bre/lib/compiler_backend_jvm.jar"
    exclude "**/bre/lib/ballerina-backend-jvm*.jar"
    exclude "**/bre/lib/bir.jar"
    exclude "**/bre/lib/jvm.jar"
    exclude "**/bre/lib/ballerina-bir*.jar"
    exclude "**/bre/lib/ballerina-jvm*.jar"
    into bootBalHome(4)
}

task copyBirJarForPack4(type: Copy) {
    dependsOn duplicatePack3AsPack4
    dependsOn configurations.pack4birJar

    from configurations.pack4birJar
    into bootBalHome(4) + "/bre/lib/"
}

task copyNativeForPack4(type: Copy) {
    dependsOn duplicatePack3AsPack4
    dependsOn configurations.pack4native

    from configurations.pack4native
    into bootBalHome(4) + "/bre/lib/"
}

task createPack4() {
    dependsOn copyBirJarForPack4
    dependsOn copyNativeForPack4
}

artifacts {
    pack1 file(bootBalHome(0)), { builtBy(createPack1) }
    pack2 file(bootBalHome(2)), { builtBy(createPack2) }
    pack3 file(bootBalHome(3)), { builtBy(createPack3) }
    pack4 file(bootBalHome(4)), { builtBy(createPack4) }
}

